---
title: "multi-member classified model"
author: "Levi John Wolf"
date: "20/07/2021"
output: html_document
---

We'll use the [scottish lip cancer data](https://www.paulamoraga.com/book-geospatial/sec-arealdataexamplespatial.html) as a typical test dataset, and this maps well onto [Bill's example (page 18)](https://www.bristol.ac.uk/media-library/sites/cmm/migrated/documents/xcmmrev2.pdf) with the same dataset. 


```{r message=F, warning=F}
library(SpatialEpi) # for the data
library(sf)
library(spdep)
library(rstan)
library(bayesplot)
data(scotland)

scotlip = st_as_sf(scotland$spatial.polygon) # use SF nowadays
for(name in colnames(scotland$data)){
  scotlip[name] = scotland$data[name] # add the columns of the data to the sf dataframe
}
wq = poly2nb(scotlip) # get queen weights
```


This stan model has a pretty customized structure, but I'll comment a few places that could be changed to be more useful for a specific situation. 
```{stan, output.var='mmmc'}
data {
  int<lower=0> n_obs; // number of observations, here 56
  int<lower=0> counts[n_obs]; // vector of outcomes, which are counts of lip cancer in this case
  vector[n_obs] exposure; // X variable in the model, modelling exposure to UV rays
  matrix[n_obs, n_obs] W; // row-standardized spatial weights matrix recording adjacencies
}

parameters {
  real constant; // beta_0 in Bill's model
  real effect; // effect of exposure on lip cancer
  real<lower=0> place_variance; // variance of the place-level random effect (a kind of residual)
  real place_re[n_obs]; // the place-level random effects
  real<lower=0> spillover_variance; // variance of the "spillover" effects on the next line
  real spillover_re[n_obs]; // spillover random effects, u_j^(3) in Bill's paper, Eq. 12
}

model {
  real log_lambda[n_obs]; // the mean of a poisson random variate
  place_variance ~ cauchy(0, 5); // prior for variance
  spillover_variance ~ cauchy(0, 5);
  place_re ~ normal(0, place_variance); // random effects for the place
  spillover_re ~ normal(0, spillover_variance); // random spillovers
  for (i in 1:n_obs) {
    log_lambda[i] = constant + effect*exposure[i] + place_re[i]; // mean of poisson is the exogenous info
    for (j in 1:n_obs) { 
      log_lambda[i] += spillover_re[j]*W[i,j]; // plus the spillovers
    }
    counts[i] ~ poisson(exp(log_lambda[i])); // and the outcome is distributed poisson
  }
}
```
With this model, we have the residuals (`place_re`) and the spillovers `spillover_re`. There is no direct spatial correlation parameter, but the variance of the random effects (and the correlation between then) is meaningful in this sense. No attempt is made at modelling the decay with *distance*, however, as is possible from a CAR/SAR covariance matrix. 

Regardless, we stuff this into a named list to use in the `rstan::sampling` function:
```{r}

data = list(n_obs = nrow(scotlip), 
            counts = scotlip$cases,
            exposure = scotlip$AFF,
            W = as.array(nb2mat(wq, zero.policy=T)))
```

And then sample the model.
```{r message=F, warning=F}
out = sampling(mmmc, data=data, show_messages=F)
mcmc_areas(out, pars=c("constant", 'effect', 'place_variance', 'spillover_variance'))
```


```